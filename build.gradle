buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        maven { url = 'https://dist.creeper.host/Sponge/maven' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '5.1.0'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'org.spongepowered.mixin'

group = _group
archivesBaseName = _name
version = _version

tasks.withType(JavaCompile).each {
    task ->
        task.options.encoding = 'UTF-8'
        task.sourceCompatibility = task.targetCompatibility = '1.8'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            from components.java
        }
    }
}

minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'
    runs {
        client = server = {
            properties 'forge.logging.console.level': 'info'
            arg "-mixin.config=" + project.archivesBaseName + ".mixins.json"
            arg '--nogui'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin { add sourceSets.main, project.archivesBaseName + ".refmap.json" }
jar {
    manifest {
        attributes([
                "Specification-Title"     : project.archivesBaseName,
                "Specification-Vendor"    : project.archivesBaseName,
                "Specification-Version"   : project.version,
                "Implementation-Title"    : project.archivesBaseName,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : project.archivesBaseName,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : project.archivesBaseName + ".mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

afterEvaluate {
    reobf {
        shadowJar {
            mappings = tasks.reobfJar.mappings
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.4-35.1.4'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

//    shadow for build || implementation for ide run || todo how to make it better?

    implementation 'org.springframework:spring-context:5.3.2'
    shadow 'org.springframework:spring-context:5.3.2'
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set('')
}

build.dependsOn shadowJar